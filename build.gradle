plugins {
    id 'io.github.goooler.shadow' version "8.1.7"
    id 'maven-publish'
    id 'java'
}


group = 'dev.unnm3d'
version = '5.4.7'


repositories {
    mavenLocal()
    maven { url 'https://repo.spicord.org/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.xenondevs.xyz/releases' }
    maven { url 'https://repo.maven.apache.org/maven2/' }
}

dependencies {
    compileOnly 'net.kyori:adventure-text-minimessage:4.17.0'
    compileOnly 'net.kyori:adventure-text-serializer-plain:4.17.0'
    compileOnly 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.3.4'

    implementation 'com.github.Exlll.ConfigLib:configlib-paper:v4.5.0'
    implementation 'xyz.xenondevs.invui:invui-core:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r7:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r11:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r15:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r16:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r17:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r18:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r19:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r20:1.41'
    implementation 'xyz.xenondevs.invui:inventory-access-r21:1.41'
    implementation 'dev.jorel:commandapi-bukkit-shade:9.6.1'
    implementation 'com.github.Anon8281:UniversalScheduler:0.1.6'


    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'io.lettuce:lettuce-core:6.4.0.RELEASE'
    compileOnly 'org.jetbrains:annotations:24.1.0'
    compileOnly 'com.zaxxer:HikariCP:5.1.0'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.github.oraxen:oraxen:1.162.0'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.8.8'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    compileOnly 'com.github.Spicord.Spicord:spicord-common:v5-SNAPSHOT'
    compileOnly 'net.dv8tion:JDA:5.0.0-beta.16'
    compileOnly 'org.apache.commons:commons-collections4:4.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
    testImplementation 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

test {
    useJUnitPlatform()
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
    dependencies {
        include dependency('com.github.Exlll.ConfigLib:configlib-paper')
        include dependency('com.github.Exlll.ConfigLib:configlib-yaml')
        include dependency("com.github.Exlll.ConfigLib:configlib-core")
        include dependency('xyz.xenondevs.invui:invui-core')
        include dependency('xyz.xenondevs.invui:inventory-access')
        include dependency('xyz.xenondevs.invui:inventory-access-r7')
        include dependency('xyz.xenondevs.invui:inventory-access-r11')
        include dependency('xyz.xenondevs.invui:inventory-access-r15')
        include dependency('xyz.xenondevs.invui:inventory-access-r16')
        include dependency('xyz.xenondevs.invui:inventory-access-r17')
        include dependency('xyz.xenondevs.invui:inventory-access-r18')
        include dependency('xyz.xenondevs.invui:inventory-access-r19')
        include dependency('xyz.xenondevs.invui:inventory-access-r20')
        include dependency('xyz.xenondevs.invui:inventory-access-r21')
        include dependency('dev.jorel:commandapi-bukkit-shade')
        include dependency('com.github.Anon8281:UniversalScheduler:0.1.6')
    }

    exclude 'colors.bin'

    relocate 'de.exlll.configlib', 'dev.unnm3d.redischat.libraries.configlib'
    relocate 'xyz.xenondevs.invui', 'dev.unnm3d.redischat.libraries.invui'
    relocate 'xyz.xenondevs.inventoryaccess', 'dev.unnm3d.redischat.libraries.inventoryaccess'
    relocate 'dev.jorel.commandapi', 'dev.unnm3d.redischat.libraries.commandapi'
    relocate 'com.github.Anon8281.universalScheduler', 'dev.unnm3d.redischat.libraries.universalScheduler'
}

def sourceJavaVersion = 17
def releaseJavaVersion = 17

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(releaseJavaVersion)
        sourceCompatibility = JavaLanguageVersion.of(sourceJavaVersion)
    }

    withSourcesJar()
    withJavadocJar()
}
compileJava.options.release.set releaseJavaVersion
compileJava.options.encoding 'UTF-8'

javadoc {
    options {
        encoding 'UTF-8'
        addStringOption('Xdoclint:none', '-quiet')
    }
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.unnm3d'
            artifactId = 'redischat'
            version = "$version"
            artifact shadowJar
            artifact javadocJar
            artifact sourcesJar
        }
    }
}

jar.dependsOn shadowJar
clean.delete "$rootDir/target"